
apply from: 'utils.gradle'
apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: 'eclipse'

import org.apache.tools.ant.filters.*

archivesBaseName = 'java-sdk'

description = 'Client library to use the IBM Watson and Alchemy Services'

task docs(type: Javadoc) {
  destinationDir = file("$buildDir/docs/all")
}

allprojects {
  apply plugin: 'java' // *Compatibility has no effect before the 'java' plug-in is applied
  sourceCompatibility = JavaVersion.VERSION_1_7
  targetCompatibility = JavaVersion.VERSION_1_7
}

subprojects {

  checkstyleTest {
    ignoreFailures = true
  }

  afterEvaluate {
    if (plugins.hasPlugin(JavaPlugin)) {
      rootProject.tasks.docs {
        source += files(sourceSets.collect { srcSet -> srcSet.allJava })
        classpath += files(sourceSets*.compileClasspath)
      }
    }
  }
}
